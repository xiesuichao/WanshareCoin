apply from: "${rootProject.rootDir}/project_config.gradle"
def isLib = rootProject.ext.isModuleLib
//def isLib = false
if (isLib) {
    apply plugin: 'com.android.library'
} else {
    apply plugin: 'com.android.application'
}
apply plugin: 'com.jakewharton.butterknife'
android {
    compileSdkVersion rootProject.ext.android.compileSdkVersion
    buildToolsVersion rootProject.ext.android.buildToolsVersion
    defaultConfig {
        minSdkVersion rootProject.ext.android.minSdkVersion
        targetSdkVersion rootProject.ext.android.targetSdkVersion
        versionCode 1
        versionName "1.0"

        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName().replace("biz-","")]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    sourceSets {
        main {
            if (!isLib) {
                manifest.srcFile 'src/demo/AndroidManifest.xml'
                // 合并 demo和main中的res
                res.srcDirs  = ['src/demo/res', 'src/main/res']
                java.srcDirs = ['src/demo/java','src/main/java']
            } else {
                manifest.srcFile 'src/main/AndroidManifest.xml'
                res.srcDirs = ['src/main/res']
                java {
                    //排除java/demo文件夹下的所有文件
                    exclude 'src/demo/**'
                }

                resources {
                    exclude 'src/demo/*'
                }
            }
        }
    }

    lintOptions {
        checkReleaseBuilds false
        abortOnError false
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation rootProject.ext.dependencies["appcompat-v7"]
    implementation rootProject.ext.dependencies["recyclerview-v7"]
    implementation rootProject.ext.dependencies["cardview-v7"]
    implementation rootProject.ext.dependencies["constraint-layout"]
    implementation rootProject.ext.dependencies["smartRefresh"]
    testImplementation rootProject.ext.dependencies["junit"]
    androidTestImplementation rootProject.ext.dependencies["test-runner"]
    androidTestImplementation rootProject.ext.dependencies["espresso-core"]
    api project(':commonlib')

    api rootProject.ext.dependencies["butterknife"]
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]
    annotationProcessor rootProject.ext.dependencies["butterknife-compiler"]
}
